UIDesigner {
    version '1.17.0'
}

allprojects {
    apply plugin: 'distribution'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.node-gradle.node'
    
    group 'org.bonitasoft.web.page'

    repositories {
        // Keep me! Required for interoperability with maven built dependencies (UI Designer)
        // https://docs.gradle.org/current/userguide/declaring_repositories.html#sec:case-for-maven-local
        mavenLocal()

        mavenCentral()
        if (project.hasProperty("extraRepositories")) {
            def extraRepositories = project.getProperties().get("extraRepositories")
            extraRepositories.split(",").each { repo ->
                def repoProperties = repo.split("::")
                maven {
                    name = repoProperties.first()
                    url = repoProperties.last()
                    //those credentials can be given using -P<repo_name>Username and -P<repo_name>Password see README.md
                    credentials(PasswordCredentials)
                }
            }
        }
    }
    
    node {
        download = true
        version = Versions.nodeVersion
        npmVersion = Versions.npmVersion
        // Use the same location for all subprojects
        workDir = file("${rootProject.projectDir}/.gradle/nodejs")
    }
    
    // Disable tar distribution declared by the distribution plugin, only distZip is used
    distTar.enabled = false

    publishing {
        repositories {
            if (project.hasProperty("altDeploymentRepository")) {
                def repoProperties = project.getProperties()."altDeploymentRepository".split("::")
                maven {
                    name = repoProperties.first()
                    url = repoProperties.last()
                    //those credentials can be given using -P<repo_name>Username and -P<repo_name>Password see README.md
                    credentials(PasswordCredentials)
                }
            }
        }
    }
}
