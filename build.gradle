plugins {
    id 'org.ajoberstar.grgit' version '4.0.1'
    id 'com.bonitasoft.gradle.bonita-release' version '0.1.53'
    
    // plugin only available internally, used for quickly deploying into a runtime environment
    // id 'com.bonitasoft.deployer.bonita-resources-deployer' version '0.1.25'
}

apply plugin: 'com.bonitasoft.gradle.bonita-uid'
apply from: 'crowdin.gradle'

UIDesigner {
  version "1.13.1"
}

allprojects {
    apply plugin: 'distribution'
    apply plugin: 'maven-publish'

    group 'org.bonitasoft.web.pages'

    repositories {
        mavenLocal()
        maven { url 'http://artifacts.rd.lan/maven/all/' }
        mavenCentral()
        jcenter()
    }

    publishing {
        repositories {
            maven {
                name 'bonitaInternal'
                if (project.version.endsWith('-SNAPSHOT')) {
                    url 'http://artifacts.rd.lan/maven/snapshots/'
                } else {
                    url 'http://artifacts.rd.lan/maven/releases/'
                }
            }
        }
    }
}

configure(subprojects.findAll { it.name.contains('page-process-list') }) {
    apply plugin: BonitaPagePlugin

    bonitaPage {
        nodeVersion = '8.9.4'
        npmVersion = '6.2.0'
        frontendBuildDir = 'dist'
    }

    publishing {
        publications {
            distributionZip(MavenPublication) { artifact distZip }
        }
    }
}
