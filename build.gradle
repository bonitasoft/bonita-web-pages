
plugins {
    id "com.moowork.node" version "1.2.0"
}

def currentDir = project.projectDir

subprojects {
    apply plugin: 'com.moowork.node'
    apply plugin: 'distribution'

    node {
        version = '8.9.4'
        npmVersion = '5.6.0'
        download = true

        workDir = file("${currentDir}/.gradle/nodejs")
        npmWorkDir = file("${currentDir}/.gradle/npm")
    }

    tasks.npm_install.configure {
        inputs.files('package.json', 'package-lock.json')
        outputs.dirs('node_modules')
    }

    def buildNpm = task buildNpm(type: NpmTask, dependsOn: npm_install) {
        args = ['run', 'build']
        inputs.files('package.json', 'package-lock.json')
        inputs.dir('src')
    }

    tasks.distZip.dependsOn buildNpm

    def cleanNpm = task cleanNpm {
        doFirst {
            delete 'dist'
        }
    }

    tasks.clean.dependsOn cleanNpm

    distributions{
        main {
            contents {
                from('resources') { into '/' }
                from('dist') { into '/resources' }
            }
        }
    }
}
