plugins {
    id "org.ajoberstar.grgit" version "4.0.1"
    id 'com.bonitasoft.gradle.bonita-release' version '0.1.53'
    id 'com.bonitasoft.gradle.maven-settings' version '0.7.3'

    // plugin only available internally, used for quickly deploying into a runtime environment
    // id 'com.bonitasoft.deployer.bonita-resources-deployer' version '0.1.25'
}

apply plugin: 'com.bonitasoft.gradle.bonita-uid'
apply from: 'crowdin.gradle'

UIDesigner {
  version "1.11.50"
}

allprojects {
    apply plugin: 'distribution'
    apply plugin: 'maven-publish'
    apply plugin: 'com.bonitasoft.gradle.maven-settings'

    group 'org.bonitasoft.web.pages'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    publishing {
        repositories {
            if (project.hasProperty("altDeploymentRepository")) {
                def repoProperties = project.getProperties()."altDeploymentRepository".split("::")
                maven {
                    name = repoProperties.first()
                    url = repoProperties.last()
                }
            }
        }
    }
}

configure(subprojects.findAll { it.name.contains('page-process-list') }) {
    apply plugin: BonitaPagePlugin

    bonitaPage {
        nodeVersion = '8.9.4'
        npmVersion = '6.2.0'
        frontendBuildDir = 'dist'
    }

    publishing {
        publications {
            distributionZip(MavenPublication) { artifact distZip }
        }
    }
}
